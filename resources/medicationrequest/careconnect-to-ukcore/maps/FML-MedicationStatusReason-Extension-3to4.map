map "https://fhir.nhs.uk/StructuredMap/MedicationStatusReason-Extension-3to4" = "R3 to R4 conversion for CareConnect MedicationStatusReason extension"
uses "http://hl7.org/fhir/StructureDefinition/MedicationRequest" alias MedicationRequest as source
uses "http://hl7.org/fhir/StructureDefinition/MedicationRequest" alias MedicationRequest as target

group MedicationRequest(source src : MedicationRequest, target tgt : MedicationRequest)  {
    src.extension as ext where $this.url='https://fhir.nhs.uk/STU3/StructureDefinition/Extension-CareConnect-MedicationStatusReason-1'
        -> tgt.statusReason as vt then {
        ext.extension as vs where $this.url='statusReason' -> tgt.statusReason as vt1 then {
           vs.value : CodeableConcept as vs1 -> tgt.statusReason as vt2 then CodeableConceptStatusReason(vs1, vt2);
        };
    };
}

group CodeableConceptStatusReason(source src : CodeableConcept, target tgt : CodeableConcept) {
    // src.coding -> tgt.coding; the short form is only woking when you have a <<type>>
    src.coding -> tgt.coding;
    src.text as text -> tgt.text = text;
}

group Coding(source src : Coding, target tgt : Coding) <<type+>> {
    src.system as system -> tgt.system = system;
    src.version as version -> tgt.version = version;
    src.code as code -> tgt.code = code;
    src.display as display -> tgt.display = display;
    src.userSelected as userSelected -> tgt.userSelected = userSelected;
}
