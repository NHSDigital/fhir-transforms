map "http://fhir.nhs.uk/StructureMap/MedicationRequestStatusReason-Extension-3to4" = "R3 to R4 conversion for CareConnect MedicationRequest StatusReason extension"
uses "http://hl7.org/fhir/StructureDefinition/MedicationRequest" alias MedicationRequest as source
uses "http://hl7.org/fhir/StructureDefinition/MedicationRequest" alias MedicationRequest as target

group MedicationRequest(source src : MedicationRequest, target tgt : MedicationRequest)  {
    src.extension as ext where $this.url='https://fhir.nhs.uk/STU3/StructureDefinition/Extension-CareConnect-MedicationStatusReason-1' or
       $this.url='https://fhir.nhs.uk/STU3/StructureDefinition/Extension-CareConnect-GPC-MedicationStatusReason-1'
       -> tgt.statusReason as vt then {
       ext.extension as vs where $this.url='statusReason' -> tgt.statusReason as vt1 then {
          vs.value : CodeableConcept as vs1 -> tgt.statusReason as vt2 then CodeableConceptStatusReason(vs1, vt2);
       };

       ext.extension as vs where $this.url='statusReason' -> tgt.extension = create('Extension') as vt then {
          vs.value : CodeableConcept as vs1 -> vt.extension = create('Extension') as vt1 then {
             vs1.coding as vs2 -> vt1 as vt2 then {
                vs2.extension as vs3 where $this.url='https://fhir.nhs.uk/STU3/StructureDefinition/Extension-coding-sctdescid' -> vt2.extension = create('Extension') as vt3, tgt.extension = vs3;
             };
          };
       };

       ext.extension as vs where $this.url='statusChangeDate' -> tgt.extension = create('Extension') as vt, 
          vt.url='http://fhir.nhs.uk/fhir/3.0/StructureDefinition/extension-statusChangeDate' then {
          vs.value : dateTime as vs1 -> vt.extension = create('Extension') as vt1, 
             vt1.url='statusChangeDate' then {
                vs.value : dateTime as vs2 -> vt1.value = create('dateTime') as vt2, vt2.value = vs2;
          };
       };

    };
}

group CodeableConceptStatusReason(source src : CodeableConcept, target tgt : CodeableConcept) {
    src.coding -> tgt.coding;
    src.text as text -> tgt.text = text;
}

group Coding(source src : Coding, target tgt : Coding) <<type+>> {
    src.system as system -> tgt.system = system;
    src.version as version -> tgt.version = version;
    src.code as code -> tgt.code = code;
    src.display as display -> tgt.display = display;
    src.userSelected as userSelected -> tgt.userSelected = userSelected;
}

