map "http://fhir.nhs.uk/StructureMap/MedicationStatementPrescribingAgency-Extension-3to4" = "R3 to R4 conversion for CareConnect MedicationStatementLastPrescribingAgency extension"
uses "http://hl7.org/fhir/StructureDefinition/MedicationStatement" alias MedicationStatement as source
uses "http://hl7.org/fhir/StructureDefinition/MedicationStatement" alias MedicationStatement as target
uses "http://hl7.org/fhir/StructureDefinition/Coding" alias Coding as source
uses "http://hl7.org/fhir/StructureDefinition/Coding" alias Coding as target

group MedicationStatement(source src : MedicationStatement, target tgt : MedicationStatement)  {
    src.extension as ext where $this.url='https://fhir.nhs.uk/STU3/StructureDefinition/Extension-CareConnect-GPC-PrescribingAgency-1' or
      $this.url='https://fhir.hl7.org.uk/STU3/StructureDefinition/Extension-CareConnect-MedicationPrescribingAgency-1'
        -> tgt.extension as tgtext, tgtext.url='https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-MedicationPrescribingOrganization' then {
        ext.value : CodeableConcept as vs -> tgtext.value = create('CodeableConcept') as vt then CodeableConceptPrescriptionType(vs, vt);
    };
}

group CodeableConceptPrescriptionType(source src, target tgt) {
    src.coding -> tgt.coding;
    src.text as text -> tgt.text = text;
}

group Coding(source src : Coding, target tgt : Coding) <<type+>> {
    src.system as system -> tgt.system = 'https://fhir.hl7.org.uk/CodeSystem/UKCore-MedicationPrescribingOrganization';
    src.version as version -> tgt.version = version;
    src.code as code -> tgt.code = code;
    src.display as display -> tgt.display = display;
    src.userSelected as userSelected -> tgt.userSelected = userSelected;
}
